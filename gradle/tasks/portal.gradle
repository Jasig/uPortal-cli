task portalClean(type: Delete, dependsOn: 'loadBuildProps') {
    group 'Portal'
    description 'Removes the deployed uPortal from the servlet container.'
    followSymlinks = true

    doFirst {
        String serverHome = buildProps.getProperty('server.home') ?: defaultServerHome
        String serverBase = buildProps.getProperty('server.base') ?: ${serverHome}
        def uPortalDeployDir = "${serverBase}/webapps/uPortal"
        logger.lifecycle("Removing deployed uPortal from servlet container at location:  ${uPortalDeployDir}")
        delete uPortalDeployDir
    }
}

task portalDeploy(dependsOn: 'loadBuildProps') {
    group 'Portal'
    description 'Deploys uPortal and all bundled war files to the embedded Tomcat servlet container'
    dependsOn 'portalClean'
    dependsOn getTasksByName('war', true)
    dependsOn getTasksByName('plutoAssemble', true)

    doLast {
        String serverHome = buildProps.getProperty('server.home') ?: defaultServerHome
        String serverBase = buildProps.getProperty('server.base') ?: ${serverHome}
        logger.lifecycle("Deploying bundled applications to the Tomcat servlet container in ${serverBase}...")

        configurations.bundled.files.each { war ->
            logger.lifecycle("  --> ${war}")
            copy {
                into "${serverBase}/webapps/${war.getName().take(war.getName().lastIndexOf('.'))}"
                with copySpec {
                    from zipTree(war)
                }
            }
        }
    }
}
